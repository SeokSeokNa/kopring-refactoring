plugins {
    id 'org.springframework.boot' version '2.6.8'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.6.21'
    //JPA를 사용하기 위해  entity 를 만들때 기본생성자가 필요한데 해당 플러그인을 추가 안하면 kotlin domain 클래스에서 오류남
    id 'org.jetbrains.kotlin.plugin.jpa' version '1.6.21'
    //@Transaction 어노테이션을 사용하려면 기본적으로 해당 메소드가 override 가 가능한 상속가능형태가 되어야하는데
    //코틀린에서는 기본적으로 모두 상속이 막혀있기에 해당 플러그인을 추가하여 상속가능한 형태로 변경한다.
    id 'org.jetbrains.kotlin.plugin.spring' version '1.6.21'

    //코틀린은 기본적으로 class , function 모두 final로 상속과 오버라이드가 기본적으로 막혀있다.
    //JPA 를 사용할때 "Proxy Lazy Fetching" 를 이용하려면 클래스가 상속이 가능해야한다.
    // 해당 플러그인을 사용하게 되면 "@Entity" 클래스들은 Decompile 하였을때 class가 열려있게 해준다.
    id "org.jetbrains.kotlin.plugin.allopen" version "1.6.21"
    //QueryDsl 관련 플러그인
    id 'org.jetbrains.kotlin.kapt' version'1.6.21'

}

// plugins, dependencies와 같은 Level (즉 build.gradle 최상단)
allOpen {
    annotation("javax.persistence.Entity")
    annotation("javax.persistence.MappedSuperclass")
    annotation("javax.persistence.Embeddable")
}

group = 'com.group'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    //테스트 코드 돌릴시 kclass reflect 오류가 나는데 해당 의존성을 추가해줘야 정상 동작함
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.6.21'
    //책 등록이나 유저등록시 json 객체를 코틀린객체로 바꿀때 사용되는 의존성
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.13.3'
    //QueryDsl 관련 의존성
    implementation 'com.querydsl:querydsl-jpa:5.0.0'
    kapt("com.querydsl:querydsl-apt:5.0.0:jpa")
    kapt("org.springframework.boot:spring-boot-configuration-processor")


    runtimeOnly 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}


compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}
